#
# setup
#
set( MAP_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}  CACHE INTERNAL "" )


#
# map server
#
if( BUILD_SERVERS )
message( STATUS "Creating target map-server" )
option( ENABLE_MAP_PCH "Enable precompiled header for map-server" ON )
set( SKILLS_UNITY_MODE "SUBFOLDERS" CACHE STRING "Unity build mode for src/map/skills (OFF|ALL|SUBFOLDERS)" )
set_property( CACHE SKILLS_UNITY_MODE PROPERTY STRINGS OFF ALL SUBFOLDERS )
file(GLOB_RECURSE MAP_HEADERS ${MAP_SOURCE_DIR}/*.hpp)
file(GLOB_RECURSE MAP_SOURCES ${MAP_SOURCE_DIR}/*.cpp)
set( DEPENDENCIES common ryml)
set( LIBRARIES ${GLOBAL_LIBRARIES} ${MYSQL_LIBRARIES} )
set( INCLUDE_DIRS ${GLOBAL_INCLUDE_DIRS} ${COMMON_BASE_INCLUDE_DIRS} ${RA_INCLUDE_DIRS} ${MYSQL_INCLUDE_DIRS} )
set( DEFINITIONS "${GLOBAL_DEFINITIONS} ${COMMON_BASE_DEFINITIONS}" )
if( WITH_PCRE )
	message( STATUS "Enabled PCRE code" )
	set( LIBRARIES ${LIBRARIES} ${PCRE_LIBRARIES} )
	set( INCLUDE_DIRS ${INCLUDE_DIRS} ${PCRE_INCLUDE_DIRS} )
	set( DEFINITIONS "${DEFINITIONS} -DPCRE_SUPPORT" )
else()
	message( STATUS "Disabled PCRE code" )
endif()
source_group( common FILES ${COMMON_BASE_HEADERS} ${COMMON_HEADERS} )
source_group( map FILES ${MAP_HEADERS} ${MAP_SOURCES} )
include_directories( ${INCLUDE_DIRS} )

# Optional unity build for skills
if( SKILLS_UNITY_MODE STREQUAL "ALL" OR SKILLS_UNITY_MODE STREQUAL "SUBFOLDERS" )
	set( SKILLS_SOURCES "" )
	foreach( src ${MAP_SOURCES} )
		if( src MATCHES "/skills/.*\\.cpp$" )
			list( APPEND SKILLS_SOURCES ${src} )
		endif()
	endforeach()
	if( SKILLS_SOURCES )
		if( SKILLS_UNITY_MODE STREQUAL "ALL" )
			set( SKILLS_UNITY_CPP ${CMAKE_CURRENT_BINARY_DIR}/skills_unity.cpp )
			file( WRITE ${SKILLS_UNITY_CPP} "// Auto-generated unity build file for skills\n" )
			foreach( src ${SKILLS_SOURCES} )
				file( APPEND ${SKILLS_UNITY_CPP} "#include \"${src}\"\n" )
			endforeach()
			list( REMOVE_ITEM MAP_SOURCES ${SKILLS_SOURCES} )
			list( APPEND MAP_SOURCES ${SKILLS_UNITY_CPP} )
		else()
			# SUBFOLDERS: one unity file per immediate subfolder under skills/
			set( SKILLS_BY_DIR "" )
			foreach( src ${SKILLS_SOURCES} )
				if( src MATCHES "/skills/([^/]+)/[^/]+\\.cpp$" )
					set( SKILLS_DIR "${CMAKE_MATCH_1}" )
					set( KEY "skills_dir_${SKILLS_DIR}" )
					list( FIND SKILLS_BY_DIR ${KEY} _idx )
					if( _idx EQUAL -1 )
						list( APPEND SKILLS_BY_DIR ${KEY} )
						set( ${KEY} "" )
					endif()
					list( APPEND ${KEY} ${src} )
				endif()
			endforeach()
			foreach( key ${SKILLS_BY_DIR} )
				string( REPLACE "skills_dir_" "" SKILLS_DIR "${key}" )
				set( SKILLS_UNITY_CPP ${CMAKE_CURRENT_BINARY_DIR}/skills_${SKILLS_DIR}_unity.cpp )
				file( WRITE ${SKILLS_UNITY_CPP} "// Auto-generated unity build file for skills/${SKILLS_DIR}\n" )
				foreach( src ${${key}} )
					file( APPEND ${SKILLS_UNITY_CPP} "#include \"${src}\"\n" )
					list( REMOVE_ITEM MAP_SOURCES ${src} )
				endforeach()
				list( APPEND MAP_SOURCES ${SKILLS_UNITY_CPP} )
			endforeach()
		endif()
	endif()
endif()

set( SOURCE_FILES ${COMMON_BASE_HEADERS} ${COMMON_HEADERS} ${MAP_HEADERS} ${MAP_SOURCES} )

#message( STATUS "map-server SOURCE_FILES=${SOURCE_FILES}")
add_executable( map-server ${SOURCE_FILES} )
#message( STATUS "map-server LIBRARIES=${LIBRARIES}, DEPENDENCIES=${DEPENDENCIES} DEFINITIONS=${DEFINITIONS}")
add_dependencies( map-server ${DEPENDENCIES} )
target_link_libraries( map-server ${LIBRARIES} ${DEPENDENCIES} )
set( MAP_SERVER_COMPILE_FLAGS "${DEFINITIONS}" )
if( ENABLE_MAP_PCH AND CMAKE_CXX_COMPILER_ID MATCHES "Clang" )
	set( MAP_PCH_HEADER ${MAP_SOURCE_DIR}/map_pch.hpp )
	if( EXISTS ${MAP_PCH_HEADER} )
		set( MAP_PCH_FILE ${CMAKE_CURRENT_BINARY_DIR}/map_pch.hpp.pch )
		set( MAP_PCH_INCLUDES "" )
		foreach( dir ${INCLUDE_DIRS} )
			list( APPEND MAP_PCH_INCLUDES "-I${dir}" )
		endforeach()
		get_target_property( RYML_INCLUDE_DIRS ryml INTERFACE_INCLUDE_DIRECTORIES )
		if( RYML_INCLUDE_DIRS )
			foreach( dir ${RYML_INCLUDE_DIRS} )
				list( APPEND MAP_PCH_INCLUDES "-I${dir}" )
			endforeach()
		endif()
		get_target_property( C4CORE_INCLUDE_DIRS c4core INTERFACE_INCLUDE_DIRECTORIES )
		if( C4CORE_INCLUDE_DIRS )
			foreach( dir ${C4CORE_INCLUDE_DIRS} )
				list( APPEND MAP_PCH_INCLUDES "-I${dir}" )
			endforeach()
		endif()
		list( APPEND MAP_PCH_INCLUDES
			"-I${CMAKE_CURRENT_SOURCE_DIR}/../../3rdparty/rapidyaml/src"
			"-I${CMAKE_CURRENT_SOURCE_DIR}/../../3rdparty/rapidyaml/ext/c4core/src"
		)
		string( TOUPPER "${CMAKE_BUILD_TYPE}" MAP_PCH_BUILD_TYPE )
		set( MAP_PCH_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${CMAKE_CXX_FLAGS_${MAP_PCH_BUILD_TYPE}}" )
		set( MAP_PCH_DEFINITIONS "${DEFINITIONS}" )
		separate_arguments( MAP_PCH_CXX_FLAGS )
		separate_arguments( MAP_PCH_DEFINITIONS )
		if( CMAKE_CXX_STANDARD )
			set( MAP_PCH_STD_FLAG "-std=gnu++${CMAKE_CXX_STANDARD}" )
		endif()
		add_custom_command(
			OUTPUT ${MAP_PCH_FILE}
			COMMAND ${CMAKE_CXX_COMPILER} ${MAP_PCH_CXX_FLAGS} ${MAP_PCH_DEFINITIONS} ${MAP_PCH_INCLUDES} ${MAP_PCH_STD_FLAG} -x c++-header ${MAP_PCH_HEADER} -o ${MAP_PCH_FILE}
			DEPENDS ${MAP_PCH_HEADER} ${COMMON_BASE_HEADERS} ${COMMON_HEADERS} ${MAP_HEADERS}
			COMMENT "Building map-server PCH"
			VERBATIM
		)
		add_custom_target( map-server-pch DEPENDS ${MAP_PCH_FILE} )
		add_dependencies( map-server map-server-pch )
		set( MAP_SERVER_COMPILE_FLAGS "${MAP_SERVER_COMPILE_FLAGS} -include-pch ${MAP_PCH_FILE}" )
	endif()
endif()
set_target_properties( map-server PROPERTIES COMPILE_FLAGS "${MAP_SERVER_COMPILE_FLAGS}" )
if( INSTALL_COMPONENT_RUNTIME )
	cpack_add_component( Runtime_mapserver DESCRIPTION "map-server" DISPLAY_NAME "map-server" GROUP Runtime )
	install( TARGETS map-server
		DESTINATION "."
		COMPONENT Runtime_mapserver )
endif( INSTALL_COMPONENT_RUNTIME )
set( TARGET_LIST ${TARGET_LIST} map-server  CACHE INTERNAL "" )
message( STATUS "Creating target map-server - done" )
endif( BUILD_SERVERS )
